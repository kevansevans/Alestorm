Class FistsBlur : Fist replaces BlurSphere {
	
	default
	{
		Weapon.SlotNumber 2;
		radius 3;
		Inventory.Amount 1;
		Scale 0.3;
		Inventory.PickupMessage "Invisi-mead, great for making the night feel... 'fuzzy'";
	}
	
	states {
	
		spawn :
		
			PINS ABCD 6 Bright;
			Loop;
	
		Select:
			PUIN A 1 FAST {
				A_Print("Invisi-mead");
				A_Raise();
				A_Overlay(2, "Select.Overlay");
			}
			Loop;
			
		Deselect:
			PUIN A 1 {
				A_Lower();
				A_Overlay(2, "Deselect.Overlay");
			}
			Loop;
			
		Deselect.Empty:
			TNT1 A 1 {
				A_Lower();
				A_Overlay(2, "Deselect.Overlay");
			}
			Loop;
	
		Ready:
			PUIN A 1 {
				A_WeaponReady();
				A_Overlay(2, "Ready.Overlay");
			}
			Loop;
	
		Fire :
			TNT1 A 0 A_Jump(128, "Fire.RightJab");
		Fire.LeftJab :
			PUIN A 1 {
				A_AlertMonsters();
				A_Overlay(2, "Fire.LeftJab.Overlay");
			}
			PUIN BC 1;
			PUIN D 1 A_CustomPunch(50, true, CPF_NOTURN, "", 0, 0, 0, "", "fists/LPUNCH");
			PUIN E 1;
			PUIN F 1 A_CustomPunch(50, true, CPF_NOTURN, "", 0, 0, 0, "", "fists/LPUNCH");
			PUIN G 1;
			PUIN H 1 A_CustomPunch(50, true, CPF_NOTURN, "", 0, 0, 0, "", "fists/LPUNCH");
			PUIN IJKLMNO 1;
			PUIN P 1 A_ReFire();
			goto Ready;
			
		Fire.RightJab :
			PUIN Q 1 {
				A_AlertMonsters();
				A_Overlay(2, "Fire.RightJab.Overlay");
			}
			PUIN RS 1;
			PUIN T 1 A_CustomPunch(50, true, CPF_NOTURN, "", 0, 0, 0, "", "fists/RPUNCHBO");
			PUIN U 1;
			PUIN V 1 A_CustomPunch(50, true, CPF_NOTURN, "", 0, 0, 0, "", "fists/RPUNCHBO");
			PUIN W 1;
			PUIN X 1 A_CustomPunch(50, true, CPF_NOTURN, "", 0, 0, 0, "", "fists/RPUNCHBO");
			PUIN XWVUTSR 1;
			PUIN Q 1 A_ReFire();
			goto Ready;
			
		AltFire :
			TNT1 A 0 A_Overlay(2, "AltFire.Smash");
			SMIN ABCDE 2;
			SMIN FGHIJ 1;
			TNT1 A 0 {
				A_PlaySound("fists/SMASH");
				A_GiveInventory("BlurSphere");
				A_GiveInventory("Ethanol");
				A_TakeInventory("FistsBlur");
			}
			goto Deselect.Empty;
		
		//Overlay states
		
		Select.Overlay :
			PUNG A 1 A_Raise;
			Loop;
			
		Deselect.Overlay :
			TNT1 A 0 A_JumpIfInventory("FistsBlur", 1, "Deselect.Overlay.NotEmpty");
			SMSM M 1 A_Lower;
			Loop;
			
		Deselect.Overlay.NotEmpty :
			PUNG A 1 A_Lower;
			Loop;
	
		Ready.Overlay:
			PUNG A 1 A_WeaponReady;
			Loop;
	
		Fire.LeftJab.Overlay :
			PUNG BBCDEFGHIJKLMNOP 1;
			Goto Ready.Overlay;
			
		Fire.RightJab.Overlay :
			PUNG QRSTUVWXXWVUTSRQ 1;
			Goto Ready.Overlay;
			
		AltFire.Smash :
			SMSH ABCDE 2;
			SMSH FGHIJ 1;
			loop;
		}
}