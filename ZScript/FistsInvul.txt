Class FistsInvul : Fist replaces InvulnerabilitySphere {
	
	default
	{
		Weapon.SlotNumber 2;
		radius 3;
		Inventory.Amount 1;
		Scale 0.3;
		Inventory.PickupMessage "Nothing makes you feel more bulletproof than... Bulletproof-Whiskey";
	}
	
	states {
	
		spawn :
		
			PINV ABCD 6 Bright;
			Loop;
	
		Select:
			PUIV A 1 FAST {
				A_Print("Bulletproof-Whiskey");
				A_Raise();
				A_Overlay(2, "Select.Overlay");
			}
			Loop;
			
		Deselect:
			PUIV A 1 {
				A_Lower();
				A_Overlay(2, "Deselect.Overlay");
			}
			Loop;
			
		Deselect.Empty:
			TNT1 A 1 {
				A_Lower();
				A_Overlay(2, "Deselect.Overlay");
			}
			Loop;
	
		Ready:
			PUIV A 1 {
				A_WeaponReady();
				A_Overlay(2, "Ready.Overlay");
			}
			Loop;
	
		Fire :
			TNT1 A 0 A_Jump(128, "Fire.RightJab");
		Fire.LeftJab :
			PUIV A 1 {
				A_AlertMonsters();
				A_Overlay(2, "Fire.LeftJab.Overlay");
			}
			PUIV BC 1;
			PUIV D 1 A_CustomPunch(50, true, CPF_NOTURN, "", 0, 0, 0, "", "fists/LPUNCH");
			PUIV E 1;
			PUIV F 1 A_CustomPunch(50, true, CPF_NOTURN, "", 0, 0, 0, "", "fists/LPUNCH");
			PUIV G 1;
			PUIV H 1 A_CustomPunch(50, true, CPF_NOTURN, "", 0, 0, 0, "", "fists/LPUNCH");
			PUIV IJKLMNO 1;
			PUIV P 1 A_ReFire();
			goto Ready;
			
		Fire.RightJab :
			PUIV Q 1 {
				A_AlertMonsters();
				A_Overlay(2, "Fire.RightJab.Overlay");
			}
			PUIV RS 1;
			PUIV T 1 A_CustomPunch(50, true, CPF_NOTURN, "", 0, 0, 0, "", "fists/RPUNCHBO");
			PUIV U 1;
			PUIV V 1 A_CustomPunch(50, true, CPF_NOTURN, "", 0, 0, 0, "", "fists/RPUNCHBO");
			PUIV W 1;
			PUIV X 1 A_CustomPunch(50, true, CPF_NOTURN, "", 0, 0, 0, "", "fists/RPUNCHBO");
			PUIV XWVUTSR 1;
			PUIV Q 1 A_ReFire();
			goto Ready;
			
		AltFire :
			TNT1 A 0 A_Overlay(2, "AltFire.Smash");
			SMIV ABCDE 2;
			SMIV FGHIJ 1;
			TNT1 A 0 {
				A_PlaySound("fists/SMASH");
				A_GiveInventory("InvulnerabilitySphere");
				A_GiveInventory("Ethanol");
				A_TakeInventory("FistsInvul");
			}
			goto Deselect.Empty;
		
		//Overlay states
		
		Select.Overlay :
			PUNG A 1 A_Raise;
			Loop;
			
		Deselect.Overlay :
			TNT1 A 0 A_JumpIfInventory("FistsInvul", 1, "Deselect.Overlay.NotEmpty");
			SMSM M 1 A_Lower;
			Loop;
			
		Deselect.Overlay.NotEmpty :
			PUNG A 1 A_Lower;
			Loop;
	
		Ready.Overlay:
			PUNG A 1 A_WeaponReady;
			Loop;
	
		Fire.LeftJab.Overlay :
			PUNG BBCDEFGHIJKLMNOP 1;
			Goto Ready.Overlay;
			
		Fire.RightJab.Overlay :
			PUNG QRSTUVWXXWVUTSRQ 1;
			Goto Ready.Overlay;
			
		AltFire.Smash :
			SMSH ABCDE 2;
			SMSH FGHIJ 1;
			loop;
		}
}